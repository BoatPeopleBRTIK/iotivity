##
# Notification build script
##

Import('env')

lib_env = env.Clone()
SConscript(env.get('SRC_DIR') + '/service/third_party_libs.scons', 'lib_env')
notification_sample_env = lib_env.Clone()

target_os = env.get('TARGET_OS')
######################################################################
# Build flags
######################################################################
notification_sample_env.AppendUnique(CPPPATH = ['../../../include'])
notification_sample_env.AppendUnique(CPPPATH = ['../../provider/inc'])
notification_sample_env.AppendUnique(CPPPATH = ['../../consumer/inc'])
notification_sample_env.AppendUnique(CPPPATH = ['../../common'])
notification_sample_env.AppendUnique(CPPPATH = ['../../../../../resource/csdk/stack/include'])
notification_sample_env.AppendUnique(CPPPATH = ['../../../../../resource/csdk/connectivity/api'])
notification_sample_env.AppendUnique(CPPPATH = ['../../../src/common'])

notification_sample_env.PrependUnique(LIBS = [
	'liboctbstack',
	'oc_logger',
	'connectivity_abstraction',
	'libcoap'
	])

if target_os not in ['windows', 'winrt']:
	notification_sample_env.AppendUnique(CXXFLAGS = ['-O2', '-g', '-Wall', '-fmessage-length=0', '-std=c++0x'])

if target_os not in ['darwin', 'ios', 'windows', 'winrt']:
	notification_sample_env.AppendUnique(LINKFLAGS = ['-Wl,--no-undefined'])

if target_os == 'linux':
	notification_sample_env.AppendUnique(LIBS = ['pthread'])

if target_os == 'android':
	notification_sample_env.AppendUnique(CXXFLAGS = ['-frtti', '-fexceptions'])
	notification_sample_env.AppendUnique(LIBS = ['gnustl_shared','log'])

	if not env.get('RELEASE'):
		notification_sample_env.AppendUnique(LIBS = ['log'])
####################################################################
# Source files and Targets
######################################################################
notification_sample_provider_env = notification_sample_env.Clone()

notification_sample_provider_env.AppendUnique(LIBS = 'libnotification_provider_wrapper')
notification_sample_provider_env.AppendUnique(LIBS = 'libnotification_provider')
notificationproviderwrapper = notification_sample_provider_env.Program('notificationproviderwrapper', 'notificationserviceprovider.cpp')
i_notificationprovider = notification_sample_provider_env.Install(env.get('BUILD_DIR'), notificationproviderwrapper)

notification_sample_consumer_env = notification_sample_env.Clone()
notification_sample_consumer_env.AppendUnique(LIBS = 'libnotification_consumer_wrapper')
notification_sample_consumer_env.AppendUnique(LIBS = 'libnotification_consumer')
notificationconsumerwrapper = notification_sample_consumer_env.Program('notificationconsumerwrapper', 'notificationserviceconsumer.cpp')
i_notificationconsumer = notification_sample_consumer_env.Install(env.get('BUILD_DIR'), notificationconsumerwrapper)
